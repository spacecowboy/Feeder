apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'

android {
  buildToolsVersion '25.0.2'

  lintOptions {
    abortOnError true
    explainIssues true
    ignoreWarnings true
    textReport true
    textOutput 'stdout'
    // Should try to remove last two here
    disable "MissingTranslation", "AppCompatCustomView", "InvalidPackage"
    // I really want some to show as errors
    error "InlinedApi"
  }

  defaultConfig {
    applicationId "com.nononsenseapps.feeder"
    versionCode 19
    versionName "1.3.12"
    compileSdkVersion 25
    minSdkVersion 18
    targetSdkVersion 25

    // For espresso tests
    testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
  }

  buildTypes {
    debug {
      minifyEnabled false
    }
    release {
      minifyEnabled false
      proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
    }
  }

  sourceSets {
    main.java.srcDirs += 'src/main/kotlin'
    test.java.srcDirs += 'src/test/kotlin'
  }

  testOptions {
    unitTests.returnDefaultValues = true
  }

  packagingOptions {
    // Rome incorrectly bundles stuff in its jar
    pickFirst 'rome-utils-1.7.1.jar'
  }

  compileOptions {
    sourceCompatibility JavaVersion.VERSION_1_7
    targetCompatibility JavaVersion.VERSION_1_7
  }
}


tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).all {
  sourceCompatibility = JavaVersion.VERSION_1_6
  targetCompatibility = JavaVersion.VERSION_1_6

  kotlinOptions {
    jvmTarget = '1.6'
    // 1.1 introduces JavaPlatform dependent functions and there is currently no way to enforce Java8 methods not to
    // be used EVER so we enforce 1.0 version of the stdlib to ensure that all methods are compatible with java6
    apiVersion = '1.0'
  }
}


dependencies {
  compile "org.jetbrains.kotlin:kotlin-stdlib-jre7:$kotlin_version"

  compile 'com.android.support.constraint:constraint-layout:1.0.2'
  compile 'com.android.support:recyclerview-v7:25.4.0'
  compile 'com.android.support:support-v4:25.4.0'
  compile 'com.android.support:appcompat-v7:25.4.0'
  compile 'com.android.support:design:25.4.0'
  // To support SDK18
  compile 'com.nononsenseapps:filepicker:4.1.0'
  // Better times
  compile 'joda-time:joda-time:2.3'
  // HTML parsing
  compile 'org.jsoup:jsoup:1.7.3'
  compile 'org.ccil.cowan.tagsoup:tagsoup:1.2.1'
  // RSS
  compile "com.rometools:rome:1.7.1"
  compile "com.rometools:rome-modules:1.7.1"
  // For better fetching
  compile 'com.squareup.okhttp3:okhttp:3.6.0'
  // Image loading
  compile 'com.github.bumptech.glide:glide:3.7.0'
  compile 'com.github.bumptech.glide:okhttp3-integration:1.4.0@aar'
  // tests
  testCompile "org.jetbrains.kotlin:kotlin-stdlib-jre7:$kotlin_version"
  testCompile "org.jetbrains.kotlin:kotlin-test-junit:$kotlin_version"
  testCompile 'junit:junit:4.12'
  testCompile 'org.mockito:mockito-core:2.7.11'

  androidTestCompile 'com.android.support:recyclerview-v7:25.4.0'
  androidTestCompile 'com.android.support:support-v4:25.4.0'
  androidTestCompile 'com.android.support:appcompat-v7:25.4.0'
  androidTestCompile 'com.android.support:design:25.4.0'
  androidTestCompile 'junit:junit:4.12'
  androidTestCompile 'com.android.support.test:runner:0.5'
  androidTestCompile 'com.android.support.test:rules:0.5'
  androidTestCompile 'com.android.support.test.espresso:espresso-core:2.2.2'
  androidTestCompile 'com.android.support.test.espresso:espresso-contrib:2.2.2'
  androidTestCompile 'com.android.support.test.uiautomator:uiautomator-v18:2.1.2'
}
