apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'

android {
  buildToolsVersion '27.0.3'

  lintOptions {
    abortOnError true
    explainIssues true
    ignoreWarnings true
    textReport true
    textOutput 'stdout'
    // Should try to remove last two here
    disable "MissingTranslation", "AppCompatCustomView", "InvalidPackage"
    // I really want some to show as errors
    error "InlinedApi", "StringEscaping"
  }

  defaultConfig {
    applicationId "com.nononsenseapps.feeder"
    versionCode 36
    versionName "1.6.8"
    compileSdkVersion 28
    minSdkVersion 18
    targetSdkVersion 28

    // For espresso tests
    testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
  }

  buildTypes {
    debug {
      minifyEnabled false
    }
    release {
      minifyEnabled false
      proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
    }
  }

  testOptions {
    unitTests.returnDefaultValues = true
  }

  packagingOptions {
    // Rome incorrectly bundles stuff in its jar
    pickFirst 'rome-utils-1.7.1.jar'
  }

  compileOptions {
    sourceCompatibility JavaVersion.VERSION_1_7
    targetCompatibility JavaVersion.VERSION_1_7
  }
}


tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).all {
  sourceCompatibility = JavaVersion.VERSION_1_6
  targetCompatibility = JavaVersion.VERSION_1_6

  kotlinOptions {
    jvmTarget = '1.6'
    // 1.1 introduces JavaPlatform dependent functions and there is currently no way to enforce Java8 methods not to
    // be used EVER so we enforce 1.0 version of the stdlib to ensure that all methods are compatible with java6
    apiVersion = '1.0'
  }
}

kotlin {
  experimental {
    coroutines "enable"
  }
}

dependencies {
  implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"

  def supportLibraryVersion = '27.1.1'

  implementation "com.android.support.constraint:constraint-layout:1.1.1"
  implementation "com.android.support:recyclerview-v7:$supportLibraryVersion"
  implementation "com.android.support:support-v4:$supportLibraryVersion"
  implementation "com.android.support:appcompat-v7:$supportLibraryVersion"
  implementation "com.android.support:design:$supportLibraryVersion"
  // To support SDK18
  implementation "com.nononsenseapps:filepicker:4.1.0"
  // Better times
  implementation "joda-time:joda-time:2.3"
  // HTML parsing
  implementation "org.jsoup:jsoup:1.7.3"
  implementation "org.ccil.cowan.tagsoup:tagsoup:1.2.1"
  // RSS
  implementation "com.rometools:rome:1.7.1"
  implementation "com.rometools:rome-modules:1.7.1"
  // JSONFeed
  implementation project(":jsonfeed-parser")
  // For better fetching
  implementation "com.squareup.okhttp3:okhttp:3.10.0"
  // For supporting missing cyphers on older platforms
  implementation "org.conscrypt:conscrypt-android:1.1.2"
  // Image loading
  implementation "com.github.bumptech.glide:glide:3.7.0"
  implementation "com.github.bumptech.glide:okhttp3-integration:1.4.0@aar"
  // Coroutines
  implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:0.22"
  // For doing coroutines on UI thread
  implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:0.22"
  // tests
  testImplementation "org.jetbrains.kotlin:kotlin-stdlib-jre7:$kotlin_version"
  testImplementation "org.jetbrains.kotlin:kotlin-test-junit:$kotlin_version"
  testImplementation "junit:junit:4.12"
  testImplementation "org.mockito:mockito-core:2.13.0"
  testImplementation "io.mockk:mockk:1.7.2"

  androidTestImplementation "com.android.support:recyclerview-v7:$supportLibraryVersion"
  androidTestImplementation "com.android.support:support-v4:$supportLibraryVersion"
  androidTestImplementation "com.android.support:appcompat-v7:$supportLibraryVersion"
  androidTestImplementation "com.android.support:design:$supportLibraryVersion"
  androidTestImplementation "junit:junit:4.12"
  androidTestImplementation "com.android.support.test:runner:1.0.2"
}
