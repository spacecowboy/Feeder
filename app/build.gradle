apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'

android {
  buildToolsVersion '27.0.1'

  lintOptions {
    abortOnError true
    explainIssues true
    ignoreWarnings true
    textReport true
    textOutput 'stdout'
    // Should try to remove last two here
    disable "MissingTranslation", "AppCompatCustomView", "InvalidPackage"
    // I really want some to show as errors
    error "InlinedApi", "StringEscaping"
  }

  defaultConfig {
    applicationId "com.nononsenseapps.feeder"
    versionCode 25
    versionName "1.4.2"
    compileSdkVersion 27
    minSdkVersion 18
    targetSdkVersion 27

    // For espresso tests
    testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
  }

  buildTypes {
    debug {
      minifyEnabled false
    }
    release {
      minifyEnabled false
      proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
    }
  }

  sourceSets {
    main.java.srcDirs += 'src/main/kotlin'
    test.java.srcDirs += 'src/test/kotlin'
  }

  testOptions {
    unitTests.returnDefaultValues = true
  }

  packagingOptions {
    // Rome incorrectly bundles stuff in its jar
    pickFirst 'rome-utils-1.7.1.jar'
  }

  compileOptions {
    sourceCompatibility JavaVersion.VERSION_1_7
    targetCompatibility JavaVersion.VERSION_1_7
  }
}


tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).all {
  sourceCompatibility = JavaVersion.VERSION_1_6
  targetCompatibility = JavaVersion.VERSION_1_6

  kotlinOptions {
    jvmTarget = '1.6'
    // 1.1 introduces JavaPlatform dependent functions and there is currently no way to enforce Java8 methods not to
    // be used EVER so we enforce 1.0 version of the stdlib to ensure that all methods are compatible with java6
    apiVersion = '1.0'
  }
}

dependencies {
  implementation "org.jetbrains.kotlin:kotlin-stdlib-jre7:$kotlin_version"

  implementation 'com.android.support.constraint:constraint-layout:1.0.2'
  implementation 'com.android.support:recyclerview-v7:27.0.2'
  implementation 'com.android.support:support-v4:27.0.2'
  implementation 'com.android.support:appcompat-v7:27.0.2'
  implementation 'com.android.support:design:27.0.2'
  // To support SDK18
  implementation 'com.nononsenseapps:filepicker:4.1.0'
  // Better times
  implementation 'joda-time:joda-time:2.3'
  // HTML parsing
  implementation 'org.jsoup:jsoup:1.7.3'
  implementation 'org.ccil.cowan.tagsoup:tagsoup:1.2.1'
  // RSS
  implementation "com.rometools:rome:1.7.1"
  implementation "com.rometools:rome-modules:1.7.1"
  // JSONFeed
  implementation project(":jsonfeed-parser")
  // For better fetching
  implementation 'com.squareup.okhttp3:okhttp:3.9.0'
  // Image loading
  implementation 'com.github.bumptech.glide:glide:3.7.0'
  implementation 'com.github.bumptech.glide:okhttp3-integration:1.4.0@aar'
  // Rx
  implementation "io.reactivex.rxjava2:rxjava:2.1.6"
  implementation 'io.reactivex.rxjava2:rxandroid:2.0.1'
  //implementation 'io.reactivex:rxkotlin:2.1.0'
  // tests
  testImplementation "org.jetbrains.kotlin:kotlin-stdlib-jre7:$kotlin_version"
  testImplementation "org.jetbrains.kotlin:kotlin-test-junit:$kotlin_version"
  testImplementation 'junit:junit:4.12'
  testImplementation 'org.mockito:mockito-core:2.7.11'

  androidTestImplementation 'com.android.support:recyclerview-v7:27.0.2'
  androidTestImplementation 'com.android.support:support-v4:27.0.2'
  androidTestImplementation 'com.android.support:appcompat-v7:27.0.2'
  androidTestImplementation 'com.android.support:design:27.0.2'
  androidTestImplementation 'junit:junit:4.12'
  androidTestImplementation 'com.android.support.test:runner:1.0.1'
}
